#!/usr/bin/env python

Import('env')

env.CXXFile('bcparser.cpp', 'bcparser.yy', YACCFLAGS = '-d --defines=${TARGET.dir}/bcparser.h')
env.CXXFile('bclexer.cpp', 'bclexer.ll', LEXFLAGS = '-Pbc_yy')
env.CXXFile('tulexer.cpp', 'tulexer.ll')

budgie_list = env.Program('budgie', source = [
    'budgie.cpp',
    'tree.cpp',
    'treeutils.cpp',
    'bcparser.cpp',
    'bclexer.cpp',
    'tulexer.cpp'])

def alias_emitter(target, source, env):
    source.append('#source/gengl/gengl.perl')
    return target, source

def alias_generator(source, target, env, for_signature):
    cmd = 'perl ' + source[-1].path + ' --mode=alias'
    for header in source[0:-1]:
        cmd += ' ' + header.path
    cmd += ' > ' + target[0].path
    return cmd

env.Append(BUDGIE = budgie_list[0])

alias_builder = env.Builder(
        generator = alias_generator,
        src_suffix = '.h',
        emitter = alias_emitter)

env.Append(BUILDERS = {
    'BudgieAlias': alias_builder
    })
