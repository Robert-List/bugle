#!/usr/bin/env python
Import('build_env', 'env')

def shared_library(target, source, version, env, **kw):
    version_parts = version.split('.')
    base = env.subst('${SHLIBPREFIX}' + target + '${SHLIBSUFFIX}')
    full = base + '.' + version
    soname = base + '.' + version_parts[0]

    shl_list = env.SharedLibrary(full, source, 
            SHLIBPREFIX = '', SHLIBSUFFIX = '',
            LINKFLAGS = '-Wl,-soname,' + soname, **kw)
    env.Command(soname, shl_list[0], 'ln -s ${SOURCE.file} $TARGET')
    return env.Command(base, shl_list[0], 'ln -s ${SOURCE.file} $TARGET')

# TODO source scanner, make this a tool?
build_env.Command(
        'data/gl.tu',
        'data/gl.c',
        'CCACHE_DISABLE=1 $CC $_CPPINCFLAGS -fdump-translation-unit -c $SOURCE && ${srcdir.path}/mvtu.sh gl.c $TARGET')

libbugleutils = shared_library(
        'bugleutils',
        [
            'gl/gltypes.c',
            'apireflect.c',
            'glwin/glwintypes.c',
            '../common/misc.c',
            '../common/protocol.c',
            '../common/protocol-win32.c',
            '../common/hashtable.c',
            '../common/linkedlist.c',
            '../budgielib/internal.c',
            '../budgielib/reflect.c',
            '../budgielib/tables.c',
            'apitables.c'
        ], '7.0.0', env)

libbugle = shared_library(
        'bugle',
        [
            env.CFile('confparse.y', YACCFLAGS = '-d')[0],
            'conflex.l',
            'interceptor.c',
            'dlopen.c',
            'filters.c',
            'stats.c',
            'statslex.l',
            env.CFile('statsparse.y', YACCFLAGS = '-d')[0],
            'log.c',
            'objects.c',
            'input.c',
            '../budgielib/addresses.c',
            'gl/glutils.c',
            'gl/glsl.c',
            'glwin/trackcontext.c',
            'gl/gldisplaylist.c',
            'gl/globjects.c',
            'gl/glextensions.c',
            'gl/glbeginend.c'
        ],
        '8.0.0', env,
        LIBS = libbugleutils)

Export('libbugle', 'libbugleutils')
