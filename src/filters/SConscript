#!/usr/bin/env python

Import('envs', 'targets', 'aspects')

filter_env = envs['host'].Clone(
        LIBS = targets['bugle'].out + targets['bugleutils'].out,
        SHLIBPREFIX = '')

if filter_env['PLATFORM'] == 'posix' and 'gcc' in filter_env['TOOLS']:
    filter_env.Append(LINKFLAGS = '-Wl,-soname,${TARGET.file}')

for filter in [
        'checks',
        'common',
        'debugger',
        'exe',
        'extoverride',
        'logstats',
        'showextensions',
        'stats_basic',
        'stats_calls',
        'stats_calltimes',
        'stats_nv',
        'stats_primitives',
        'trace',
        'unwindstack',
        'validate']:
    filter_env.LoadableModule(filter, [filter + '.c'])

if aspects['gltype'] == 'gl':
    # These currently only build on desktop GL
    for filter in [
            'camera',
            'modify',
            'stats_fragments',
            'showstats']:
        filter_env.LoadableModule(filter, [filter + '.c'])
    filter_env.LoadableModule('eps', ['eps.c', '../gl2ps/gl2ps.c'])
