#!/usr/bin/env python
import os

Import('aspects', 'version')

# TODO: single.html doesn't see bugle.css in this setup
if 'docs' in aspects['parts']:
    env = Environment(
            ENV = os.environ,
            DOCBOOKFLAGS = '--stringparam bugle.version ' + version,
            tools = ['default', 'docbook'])
    if GetOption('bugle_short'):
        for i in ['DOCBOOK']:
            env[i + 'COMSTR'] = i + ' $TARGET'

    env.DocBook(
            target = 'html/single.html',
            source = ['xhtml-single.xsl', 'bugle.xml'])
    env.Command('html/bugle.css', 'bugle.css', [Copy('$TARGET', '$SOURCE')])

    chunked = env.DocBook(
            target = 'html/chunked/index.html',
            source = ['xhtml-chunk.xsl', 'bugle.xml'])
    env.Command('html/chunked/bugle.css', 'bugle.css', [Copy('$TARGET', '$SOURCE')])
    env.Clean(chunked, 'html/chunked')

    env.Install(aspects['docdir'], 'html/single.html')
    env.Install(aspects['docdir'], 'html/chunked')
